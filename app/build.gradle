apply plugin: 'com.android.application'
apply plugin: 'android-aspectjx'


aspectjx {
    exclude 'android.support'
}

def gitVersionCode() {
    def cmd = 'git rev-list HEAD --count'
    cmd.execute().text.trim()
}

def getVersionTime() {
    return new Date().format("yyyyMMdd", TimeZone.getTimeZone("CST"))
}


android {
    compileSdkVersion 28
    flavorDimensions "default"
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    defaultConfig {
        applicationId "com.easydorm.easydorm"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "0.0.4"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
        develop {
            buildConfigField "int", "ENV_TYPE", "0"
            applicationIdSuffix ".dev"
            manifestPlaceholders = [
                    app_name: "易舍(开发版)"
            ]
        }
        product {
            buildConfigField "int", "ENV_TYPE", "1"
            manifestPlaceholders = [
                    app_name: "易舍"
            ]
        }
    }
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def gitVersion = gitVersionCode()
            def versionTime = getVersionTime()
            if (variant.buildType.name == 'release') {
                output.versionNameOverride = "v${variant.versionName}.${gitVersion}"
                output.versionCodeOverride = gitVersion.toInteger()
            } else if (variant.buildType.name == 'debug') {
                output.versionNameOverride = "v${variant.versionName}.${gitVersion}_${versionTime}"
                output.versionCodeOverride = gitVersion.toInteger()
            }
        }
    }
    android.applicationVariants.all { variant ->
        variant.outputs.all {
            def gitVersion = gitVersionCode()
            def versionTime = getVersionTime()
            outputFileName = "EDorm_v${variant.versionName}.${gitVersion}_${versionTime}_${variant.productFlavors[0].name}_${variant.buildType.name}.apk"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    //noinspection GradleCompatible
    implementation 'androidx.appcompat:appcompat:1.1.0-alpha01'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha2'
    implementation 'com.android.support:preference-v7:28.1.0'

    //walle build
    implementation 'com.meituan.android.walle:library:1.1.6'

    //network
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'

    //permission
    implementation 'pub.devrel:easypermissions:2.0.0'

    //CircleImageView
    implementation 'de.hdodenhof:circleimageview:2.2.0'

    //takePhoto
    implementation 'org.devio.takephoto:takephoto_library:4.1.0'

    //glide
    implementation 'com.github.bumptech.glide:glide:4.8.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.8.0'

    //BaseRecyclerViewAdapterHelper
    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.31'

    //app introduction
    implementation 'com.github.paolorotolo:appintro:4.1.0'

    //aspect
    implementation 'org.aspectj:aspectjrt:1.8.13'

    //log
    implementation 'com.orhanobut:logger:2.2.0'

    //butterknife
    implementation 'com.jakewharton:butterknife:9.0.0-rc3'
    annotationProcessor 'com.jakewharton:butterknife-compiler:9.0.0-rc3'

    //rxjava
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.3'

    //debug OOM
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.6.2'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.6.2'

    implementation 'com.google.android.material:material:1.1.0-alpha02'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
}
