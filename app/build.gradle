apply plugin: 'com.android.application'
apply plugin: 'android-aspectjx'


aspectjx {
    exclude 'android.support'
}

def gitVersionCode() {
    def cmd = 'git rev-list HEAD --count'
    cmd.execute().text.trim()
}

def getVersionTime() {
    return new Date().format("yyyyMMdd", TimeZone.getTimeZone("CST"))
}


android {
    compileSdkVersion 28
    flavorDimensions "default"
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    defaultConfig {
        applicationId "com.easydorm.easydorm"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            minifyEnabled false
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
        develop {
            manifestPlaceholders = [
                    app_name: "易舍(开发版)"
            ]
        }
        product {
            manifestPlaceholders = [
                    app_name: "易舍"
            ]
        }
    }
    applicationVariants.all {
        applicationVariants.all { variant ->
            if (variant.buildType.name == 'release') {
                variant.outputs.each { output ->
                    def gitVersion = gitVersionCode()
                    output.versionNameOverride = "v0.0.3." + gitVersion + "_" + getVersionTime()
                    output.versionCodeOverride = gitVersion.toInteger()
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    //noinspection GradleCompatible
    implementation 'androidx.appcompat:appcompat:1.1.0-alpha01'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha2'

    //network and json
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'

    //image
    implementation 'de.hdodenhof:circleimageview:2.2.0'
    implementation 'org.devio.takephoto:takephoto_library:4.1.0'
    implementation 'com.github.bumptech.glide:glide:4.8.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.8.0'

    //app intro
    implementation 'com.github.paolorotolo:appintro:4.1.0'

    //aspect
    implementation 'org.aspectj:aspectjrt:1.8.13'

    //log
    implementation 'com.orhanobut:logger:2.2.0'

    //annotation
    implementation 'com.jakewharton:butterknife:9.0.0-rc3'
    annotationProcessor 'com.jakewharton:butterknife-compiler:9.0.0-rc3'

    //rx
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.3'

    //debug
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.6.2'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.6.2'

    implementation 'com.google.android.material:material:1.1.0-alpha02'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
}
